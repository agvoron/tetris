// from jpackage guide
plugins { id 'org.beryx.runtime' version '1.8.4' }

sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["../core/assets"]

// from jpackage guide
// project.ext.mainClassName = "agvoron.tetris.desktop.DesktopLauncher"
mainClassName = "agvoron.tetris.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../core/assets")

// from jpackage guide - rename to runGame from run
task runGame(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    // from jpackage guide
    destinationDirectory = file("$buildDir/lib")
}

jpackageImage.dependsOn dist // from jpackage guide

dist.dependsOn classes

eclipse.project.name = appName + "-desktop"

// from jpackage guide
runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base',
               'java.desktop',
               'jdk.unsupported']
    distDir = file(buildDir)

    jpackage {
        jpackageHome = 'C:/Program Files/Eclipse Adoptium/jdk-17.0.2.8-hotspot'
        mainJar = dist.archiveFileName.get()
        //imageOptions = ["--icon", file("../icons/icon.ico")] // only for windows
    }
}